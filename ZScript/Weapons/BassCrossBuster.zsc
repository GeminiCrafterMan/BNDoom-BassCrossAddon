class BassCrossBuster : BusterBase
{
	// No need for A_SetElementTranslation.
	// No need for A_EmotionCheck.
	// No need for A_FireChargeShot... YET. If I implement a way to swap between Gold and Silver, then yeah.
	Default
	{
		Tag "BassCross Buster";
	}
	States
	{
		Ready:
			NBST A 1 {
				A_WeaponReady(WRF_ALLOWUSER1);	// no need for zoom or reload, he has no styles (he has no grace)
				A_OverlayTranslation(OverlayID(), "BCMMGold");
			}
			Loop;
		Select:
			TNT1 A 0 A_Overlay(99,"TopLayer");
			TNT1 A 0 A_OverlayTranslation(OverlayID(), "BCMMGold");
			TNT1 A 0 A_SetInventory("PrimaryCharge", 0);
			NBST A 1 A_Raise;
			Loop;
		Charge:
			NBST A 1 A_GiveInventory("PrimaryCharge", 1);
			NBST A 0 A_Refire("Charge");
			NBST A 0 A_JumpIfInventory("PrimaryCharge", 50, "FireChargeShot");
			TNT1 A 0 { return ResolveState("FireNormalShot"); }
		AltFire:
		FireNormalShot:
			NBST A 1 A_SetInventory("PrimaryCharge", 0);
			NBST A 0 A_GiveInventory("Firing", 1);
			NBST B 1 Bright {
				A_FireProjectile("UnchargedBusterTracer", frandom(-4.275, 4.275), 0, 4, 0, 0, frandom(-4.275, 4.275));
				A_FireProjectile("UnchargedBusterTracer", frandom(-4.275, 4.275), 0, 4, 0, 0, frandom(-4.275, 4.275));
				A_FireProjectile("UnchargedBusterTracer", frandom(-4.275, 4.275), 0, 4, 0, 0, frandom(-4.275, 4.275));
				A_StartSound("weapons/gun", CHAN_WEAPON);
				A_Overlay(PSP_FLASH, "Flash");	// Flash layer
			}
			TNT1 A 0 { return ResolveState("Fired"); }
		FireChargeShot:
			NBST A 1 {
				A_SetInventory("PrimaryCharge", 0);
				A_GiveInventory("Firing", 1);
			}
			NBST B 1 Bright {
				A_FireProjectile("HelzRolling", 11.25, 0, -4);
				A_FireProjectile("HelzRolling", -11.25, 0, 4);
//				A_StartSound("weapons/helzrolling", CHAN_WEAPON);
				A_Overlay(PSP_FLASH, "Flash");	// Flash layer
			}
			TNT1 A 0 { return ResolveState("Fired"); }

			// Subroutine for charge effect
		ChargeEffectFunction:
			TNT1 A 1;
			// if charge is full, play fully-charged animation
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",50,"FullChargeAnim");
			// if charge is starting, play not-charged animation
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",1,"StartChargeAnim");
			stop;

			//Subroutine for charge sound
		ChargeSoundFunction:
			TNT1 A 1;
			//If charge is full, don't play any sound
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",50,"ChargeSoundFunction");
			//Upon reaching 99 charge, play the charged sound
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",49,"ChargeSound3");
			//With 5 or more, stay silent.
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",4,"ChargeSoundFunction");
			//Upon reaching 3 charge, play chargesound1
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",3,"ChargeSound2");
			//If there's any charge, restart.
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",1,"ChargeSoundFunction");
			stop;

		Flash:
			BSFL A 1 Bright A_OverlayTranslation(OverlayID(), "BCMMGold");
			BCFL ABC 1 Bright A_OverlayTranslation(OverlayID(), "BCMMGold"); // Triple Buster flash
			Stop;
	}
}